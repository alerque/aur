# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Fabio 'Lolix' Loli <lolix@disroot.org>

pkgname=gittyup
pkgver=1.0.0
pkgrel=0
pkgdesc='Graphical Git client (GitAhead fork)'
url="https://github.com/Murmele/${pkgname^}"
arch=(x86_64)
license=(MIT)
depends=(cmark
         libssh2
         libsecret
         qt5-base)
makedepends=(cmake
             git
             libgnome-keyring
             ninja
             qt5-tools
             qt5-translations)
optdepends=('git-lfs: git-lfs support'
            'libgnome-keyring: for GNOME Keyring for auth credentials'
            'qt5-translations: translations')
source=("${pkgname}::git+https://github.com/Murmele/Gittyup.git#tag=${pkgname}_v${pkgver}"
        "git+https://github.com/stinb/libgit2.git"
        "git+https://github.com/git/git.git"
        "git+https://github.com/libssh2/libssh2.git"
        "git+https://github.com/hunspell/hunspell.git"
        'gittyup.desktop')
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'fb900e4d101c19a5b3e1dc4c7619780e1d38fd78d0b9d951e794fbf36eebf21a')

prepare() {
    cd "${pkgname}"
    git submodule init
    git config 'submodule.dep/libgit2/libgit2.url' "${srcdir}/libgit2"
    git config 'submodule.dep/git/git.url' "${srcdir}/git"
    git config 'submodule.dep/libssh2/libssh2.url' "${srcdir}/libssh2"
    git config 'submodule.dep/hunspell/hunspell.url' "${srcdir}/hunspell"
    git -c submodule.dep/openssl/openssl.update=none -c submodule.dep/cmark/cmark.update=none submodule update
}

build() {
    cmake \
        -G Ninja \
        -Wno-dev \
        -DCMAKE_BUILD_TYPE=None \
        -DCMAKE_INSTALL_PREFIX=/usr/lib/gittyup \
        -DCMAKE_INSTALL_MANDIR=/usr/share/man \
        -DENABLE_REPRODUCIBLE_BUILDS=ON \
        -DBUILD_SHARED_LIBS=OFF \
		-B build \
		-S "${pkgname}"

	ninja -C build
}

package() {
    cd "${srcdir}/${pkgname}"
    DESTDIR="${pkgdir}" ninja -C ../build install

    rm -rf "${pkgdir}/usr/lib/gittyup/"*.so.*
    install -d "${pkgdir}/usr/bin"
    ln -s /usr/lib/gittyup/Gittyup "${pkgdir}/usr/bin/gittyup"

    install -Dm644 LICENSE.md "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    install -Dm644 ../gittyup.desktop "${pkgdir}/usr/share/applications/gittyup.desktop"

    install -Dm644 rsrc/Gittyup.iconset/gittyup_logo.svg "${pkgdir}/usr/share/icons/hicolor/scalable/apps/gittyup.svg"
    for s in 16x16 32x32 64x64 128x128 256x256 512x512; do
        install -Dm0644 "rsrc/Gittyup.iconset/icon_$s.png" "icons/hicolor/$s/apps/$pkgname.png"
    done

    rm -rf "${pkgdir}/usr/share/man" # libssh2 man pages
}
