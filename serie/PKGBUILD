# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=serie
pkgver=0.1.0
pkgrel=1
pkgdesc='A rich git commit graph in your terminal'
arch=(x86_64 i686)
url="https://github.com/lusingander/$pkgname"
license=(MIT)
depends=(gcc-libs
         git
         glibc)
makedepends=(cargo)
_archive="$pkgname-$pkgver"
source=("$url/archive/v$pkgver/$_archive.tar.gz")
sha256sums=('0b137996790881805d184ba7cf5f9bc078b703861d8ebf735ba253851c159250')

prepare() {
	cd "$_archive"
	cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

_srcenv() {
	cd "$_archive"
	export RUSTUP_TOOLCHAIN=stable
	export CARGO_TARGET_DIR=target
	export CFLAGS+=' -ffat-lto-objects'
}

build() {
	_srcenv
	cargo build --frozen --release --all-features
}

check() {
	_srcenv
	cargo test --frozen --all-features
}

package () {
	cd "$_archive"
	install -Dm0755 -t "$pkgdir/usr/bin/" "target/release/$pkgname"
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
